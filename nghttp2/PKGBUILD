# Maintainer: Zhuoyun Wei <wzyboy@wzyboy.org>

pkgname=nghttp2
pkgver=0.7.11
_pkgver=v0.7.11
pkgrel=1
pkgdesc="HTTP/2 C Library"
arch=('i686' 'x86_64')
url="https://nghttp2.org/"
license=('MIT')
depends=('openssl' 'libev' 'zlib' 'spdylay' 'libxml2' 'jansson' 'jemalloc' 'cython' 'python2' 'boost')
source=("https://github.com/tatsuhiro-t/nghttp2/releases/download/${_pkgver}/nghttp2-${pkgver}.tar.gz")
backup=('etc/nghttpx/nghttpx.conf')
sha256sums=('c3950bf9315de13dbbf4f2afbdf7e5e87a717700865516accc919d6bff8085a6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
   --enable-app \
   --enable-hpack-tools \
   --enable-examples \
   --enable-python-bindings \
   --enable-asio-lib \
   --with-jemalloc \
   --with-spdylay \
   --with-cython=/usr/bin/cython \
   --with-boost=yes \
   
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 nghttpx.conf.sample "$pkgdir/etc/nghttpx/nghttpx.conf"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/nghttp2/COPYING"

  # Install sample http2 implementations & other utils
  find ./examples/.libs -type f -executable -exec install -m755 {\} "$pkgdir/usr/bin/" \;
  
  # Install python bindings
  cd python
  python2 setup.py install --root="$pkgdir" --optimize=1
}
