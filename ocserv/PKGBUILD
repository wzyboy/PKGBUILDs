# Maintainer: Zhuoyun Wei <wzyboy@wzyboy.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=ocserv
pkgver=0.10.4
pkgrel=1
pkgdesc="OpenConnect VPN Server"
arch=('i686' 'x86_64')
url="http://www.infradead.org/ocserv/"
license=('GPL2')
depends=('gnutls')
backup=('etc/ocserv/ocserv.conf' 'etc/ocserv/passwd')
source=("ftp://ftp.infradead.org/pub/ocserv/ocserv-${pkgver}.tar.xz")
sha256sums=('4b58a57d5d3b19dd15bea5f307939d956625a31522e8959d291921c7e1263422')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm0644 doc/sample.config "$pkgdir/etc/ocserv/ocserv.conf"
  install -Dm0600 doc/sample.passwd "$pkgdir/etc/ocserv/passwd"
  install -Dm0644 doc/systemd/standalone/ocserv.service "$pkgdir/usr/lib/systemd/system/ocserv.service"
}
