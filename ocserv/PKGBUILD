# Maintainer: Zhuoyun Wei <wzyboy@wzyboy.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=ocserv
pkgver=0.10.3
pkgrel=1
pkgdesc="OpenConnect VPN Server"
arch=('i686' 'x86_64')
url="http://www.infradead.org/ocserv/"
license=('GPL2')
depends=('gnutls')
backup=('etc/ocserv/ocserv.conf' 'etc/ocserv/passwd')
source=("ftp://ftp.infradead.org/pub/ocserv/ocserv-${pkgver}.tar.xz")
sha256sums=('84a33bd0852f85ea0063d15b1ec7fb7cbc8dcf1b653b7c040190f157d25e02c2')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm0644 doc/sample.config "$pkgdir/etc/ocserv/ocserv.conf"
  install -Dm0600 doc/sample.passwd "$pkgdir/etc/ocserv/passwd"
  install -Dm0644 doc/systemd/standalone/ocserv.service "$pkgdir/usr/lib/systemd/system/ocserv.service"
}
sha256sums=('f772855a35e43683604a9be87841f652ecbcd7a7169dc086d47ebf0e1afe519f')
