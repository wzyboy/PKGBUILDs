# Maintainer: Zhuoyun Wei <wzyboy@wzyboy.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=ocserv
pkgver=0.10.5
pkgrel=1
pkgdesc="OpenConnect VPN Server"
arch=('i686' 'x86_64')
url="http://www.infradead.org/ocserv/"
license=('GPL2')
depends=('gnutls')
backup=('etc/ocserv/ocserv.conf' 'etc/ocserv/passwd')
source=("ftp://ftp.infradead.org/pub/ocserv/ocserv-${pkgver}.tar.xz")
sha256sums=('62a2b087f21b257a1ea433c12f6937d2a2f5ef30eedbe4739b0407405de474b8')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm0644 doc/sample.config "$pkgdir/etc/ocserv/ocserv.conf"
  install -Dm0600 doc/sample.passwd "$pkgdir/etc/ocserv/passwd"
  install -Dm0644 doc/systemd/standalone/ocserv.service "$pkgdir/usr/lib/systemd/system/ocserv.service"
}
